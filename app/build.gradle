import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
}
apply from: common

//load keystore.properties
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(file("../secret/keystore.properties")))

android {
    signingConfigs {
        'default' {
            storeFile file("../secret/hwei.jks")
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    defaultConfig {
        applicationId "com.hwei.demo"
        signingConfig signingConfigs.'default'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                // 此处指定生成的apk文件名
                outputFileName = "demo-${variant.name}${variant.versionName}-${getCurrentTime()}.apk"
            }
    }
}

static String getCurrentTime(){
    return new SimpleDateFormat("yyyy-MM-dd").format(new Date())
}

dependencies {
    implementation project(":home")
    implementation project(":system")
    implementation project(":me")
}